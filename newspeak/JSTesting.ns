class JSTesting usingPlatform: platform minitest: minitest = (
|
	private js = platform js.
	private TestContext = minitest TestContext.
|) (
public class JSTests = TestContext (
) (
public testIsKindOfJSAlien = (
	assert: js global isKindOfJSAlien.
	assert: js undefined isKindOfJSAlien.
	assert: (js global at: 'Object') new isKindOfJSAlien.
	deny: nil isKindOfJSAlien.
	deny: false isKindOfJSAlien.
	deny: true isKindOfJSAlien.
	deny: 0 isKindOfJSAlien.
)
public testJSEval = (
	assert: (js global eval: 'null') equals: nil.
	assert: (js global eval: 'false') equals: false.
	assert: (js global eval: 'true') equals: true.
	assert: (js global eval: '42') equals: 42.
	assert: (js global eval: '42.5') equals: 42.5 asFloat.
	assert: (js global eval: '"string"') equals: 'string'.
)
public testJSFloat = (
	| n |
	n:: 0.5 asFloat.
	assert: (js global Number: n) equals: n.
	n:: Float parse: 'Infinity'.
	assert: (js global Number: n) equals: n.
	n:: Float parse: '-Infinity'.
	assert: (js global Number: n) equals: n.
	n:: Float parse: 'NaN'.
	assert: (js global Number: n) isNaN.
)
public testJSInstanceOf = (
	| JSObject JSFunction object function |
	JSObject:: js global at: 'Object'.
	JSFunction:: js global at: 'Function'.

	object:: JSObject new.
	assert: (object instanceOf: JSObject) equals: true.
	assert: (object instanceOf: JSFunction) equals: false.
	should: [object instanceOf: object] signal: Exception.
	should: [object instanceOf: 0] signal: Exception.

	function:: JSObject at: 'create'.
	assert: (function instanceOf: JSObject) equals: true.
	assert: (function instanceOf: JSFunction) equals: true.
	should: [function instanceOf: function] signal: Exception.
	should: [function instanceOf: 0] signal: Exception.
)
public testJSInteger = (
	assert: (js global Number: 0) equals: 0.
	assert: (js global Number: 1) equals: 1.
	assert: (js global Number: -1) equals: -1.

	assert: (js global Number: 16r7F) equals: 16r7F.
	assert: (js global Number: 16rEF) equals: 16rEF.
	assert: (js global Number: 16rFF) equals: 16rFF.
	assert: (js global Number: 16r7FFF) equals: 16r7FFF.
	assert: (js global Number: 16rEFFF) equals: 16rEFFF.
	assert: (js global Number: 16rFFFF) equals: 16rFFFF.
	assert: (js global Number: 16r7FFFFFFF) equals: 16r7FFFFFFF.
	assert: (js global Number: 16rEFFFFFFF) equals: 16rEFFFFFFF.
	assert: (js global Number: 16rFFFFFFFF) equals: 16rFFFFFFFF.
	assert: (js global Number: 16r7FFFFFFFFFFF) equals: 16r7FFFFFFFFFFF.
	assert: (js global Number: 16rEFFFFFFFFFFF) equals: 16rEFFFFFFFFFFF.
	assert: (js global Number: 16rFFFFFFFFFFFF) equals: 16rFFFFFFFFFFFF.

	assert: (js global Number: -16r7F) equals: -16r7F.
	assert: (js global Number: -16rEF) equals: -16rEF.
	assert: (js global Number: -16rFF) equals: -16rFF.
	assert: (js global Number: -16r7FFF) equals: -16r7FFF.
	assert: (js global Number: -16rEFFF) equals: -16rEFFF.
	assert: (js global Number: -16rFFFF) equals: -16rFFFF.
	assert: (js global Number: -16r7FFFFFFF) equals: -16r7FFFFFFF.
	assert: (js global Number: -16rEFFFFFFF) equals: -16rEFFFFFFF.
	assert: (js global Number: -16rFFFFFFFF) equals: -16rFFFFFFFF.
	assert: (js global Number: -16r7FFFFFFFFFFF) equals: -16r7FFFFFFFFFFF.
	assert: (js global Number: -16rEFFFFFFFFFFF) equals: -16rEFFFFFFFFFFF.
	assert: (js global Number: -16rFFFFFFFFFFFF) equals: -16rFFFFFFFFFFFF.
)
public testJSMath = (
	| Math n |
	Math:: js global at: 'Math'.

	n:: Math random.
	assert: n isKindOfFloat.
	assert: n >= 0.0 asFloat.
	assert: n < 1.0 asFloat.

	n:: Math at: 'E'.
	assert: n isKindOfFloat.
	assert: n > 2.718 asFloat.
	assert: n < 2.719 asFloat.
)
public testJSObject = (
	| JSObject object |
	JSObject:: js global at: 'Object'.
	object:: JSObject new.

	should: [object methodDoesNotExist] signal: Exception.

	assert: (object at: 'property' put: 42) equals: 42.
	assert: (object at: 'property') equals: 42.
	assert: (object includesKey: 'property') equals: true.
	assert: (object removeKey: 'property') equals: true.
	assert: (object includesKey: 'property') equals: false.
)
public testJSString = (
	(* String argument *)
	assert: (js global parseInt: '42') equals: 42.

	(* String return value *)
	assert: ((js global at: 'Math') toString) equals: '[object Math]'.
)
public testJSUndefined = (
	assert: js undefined isUndefined.
	deny: js global isUndefined.
	assert: (js global at: 'DoesNotExist') isUndefined.
	deny: (js global at: 'Object') isUndefined.
	assert: js undefined printString equals: 'undefined'.
)
) : (
TEST_CONTEXT = ()
)
) : (
)
