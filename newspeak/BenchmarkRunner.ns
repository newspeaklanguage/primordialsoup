(*Infrastructure to run a set of benchmarks and gather average run times.

Copyright 2012 Google Inc.
Copyright 2013 Ryan Macnak

Licensed under the Apache License, Version 2.0 (the ''License''); you may not use this file except in compliance with the License.  You may obtain a copy of the License at  http://www.apache.org/licenses/LICENSE-2.0*)
class BenchmarkRunner packageUsing: manifest = (
|
	benchmarks = {
		manifest ClosureDefFibonacci.
		manifest ClosureFibonacci.
		manifest DeltaBlue.
		manifest MethodFibonacci.
		manifest NLRImmediate.
		manifest NLRLoop.
		manifest ParserCombinators.
		manifest Richards.
		manifest SlotRead.
		manifest SlotWrite.
		manifest Splay.
	}.
|) (
class Benchmarking usingPlatform: p = (|
private Stopwatch = p time Stopwatch.
private List = p collections List.
private cachedPlatform = p.
|) (
measure: block forAtLeast: nanoseconds = (
	| runs stopwatch elapsed score |
	runs:: 0.
	stopwatch:: Stopwatch new start.

	[block value.
	runs:: runs + 1.
	elapsed:: stopwatch elapsedNanoseconds.
	elapsed < nanoseconds] whileTrue.

	score:: runs * 1000000000 asFloat / elapsed.
	^score asStringFixed: 1
)
public report = (
	benchmarks do:
		[:benchmark |
		| b score |
		b:: benchmark usingPlatform: cachedPlatform.
		self measure: [b bench] forAtLeast: 3000000.
		score:: measure: [b bench] forAtLeast: 20000000.
		(benchmark name, ': ', score) out].
)
) : (
)
public main: p args: argv = (
	(Benchmarking usingPlatform: p) report
)
) : (
)
